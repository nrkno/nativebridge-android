apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

def getGitHash() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def runId = System.getenv("GITHUB_RUN_NUMBER") ?: "0"

def versionNumber = "1.0.${runId}-${getGitHash()}" as String

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    }
}

android {
    namespace 'no.nrk.nativebridge'
    compileSdk 33

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 33
        versionName "$versionNumber"
        multiDexEnabled true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lintOptions {
        abortOnError true
        textReport true
        textOutput 'stdout'
    }
}

ext {
    jackson = "2.8.9"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    api "com.fasterxml.jackson.core:jackson-core:$jackson"
    api "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson"
}

afterEvaluate {
    publishing {
        publications {
            mavenPublish(MavenPublication) {
                groupId 'no.nrk'
                artifactId 'nativebridge'
                version = versionNumber
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/nrkno/nativebridge-android"
                credentials {
                    username System.getenv("GITHUB_ACTOR")
                    password System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
